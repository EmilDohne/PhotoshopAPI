cmake_minimum_required (VERSION 3.20)
project(PhotoshopAPIBuild)

# --------------------------------------------------------------------------
# Configurable options

option(PSAPI_BUILD_STATIC "Build a static library version of the PhotoshopAPI" ON)
option(PSAPI_BUILD_TESTS "Build the tests associated with the PhotoshopAPI" ON)
option(PSAPI_BUILD_EXAMPLES "Build the examples associated with the PhotoshopAPI" ON)
option(PSAPI_BUILD_BENCHMARKS "Build the benchmarks associated with the PhotoshopAPI" ON)
option(PSAPI_BUILD_DOCS "Builds the documentation, requires some external installs which are documented in the README.md" ON)

# --------------------------------------------------------------------------
# Build setup

list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_SOURCE_DIR}/cmake")
set (CMAKE_CXX_STANDARD 20)

# Add zlib-ng
set(ZLIB_ENABLE_TESTS OFF)
set(ZLIBNG_ENABLE_TESTS OFF)
set(WITH_GZFILEOP OFF)
add_subdirectory (thirdparty/zlib-ng EXCLUDE_FROM_ALL)

# Trick blosc2 detection of zlib-ng
add_library(ZLIB_NG::ZLIB_NG ALIAS zlibstatic)
get_target_property(ZLIB_NG_INCLUDE_DIR zlibstatic INTERFACE_INCLUDE_DIRECTORIES)
set(ZLIB_NG_LIBRARY $<TARGET_LINKER_FILE:zlibstatic>)
set(PREFER_EXTERNAL_ZLIB ON)    # Tell blosc2 to use our copy of zlib-ng

# Add blosc2
set(BUILD_TESTS OFF)
set(BUILD_FUZZERS OFF)
set(BUILD_BENCHMARKS OFF)
set(BUILD_EXAMPLES OFF)
add_subdirectory (thirdparty/c-blosc2 EXCLUDE_FROM_ALL)

# Add doctest
add_library(doctest INTERFACE)
target_include_directories(doctest INTERFACE thirdparty/doctest)

# --------------------------------------------------------------------------
# Projects

if(PSAPI_BUILD_STATIC)
    add_subdirectory(PhotoshopAPI)
endif()
if(PSAPI_BUILD_TESTS)
    add_subdirectory(PhotoshopTest)
endif()
if(PSAPI_BUILD_BENCHMARKS)
    add_subdirectory(PhotoshopBenchmark)
endif()
if(PSAPI_BUILD_EXAMPLES)
    add_subdirectory(PhotoshopExamples/CreateSimpleDocument)
    add_subdirectory(PhotoshopExamples/CreateGroups)
    add_subdirectory(PhotoshopExamples/AddLayerMasks)
    add_subdirectory(PhotoshopExamples/ModifyLayerStructure)
endif()
if(PSAPI_BUILD_DOCS)
    add_subdirectory(docs/doxygen)
endif()
